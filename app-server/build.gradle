buildscript {
	ext {
		springBootVersion = '2.0.2.RELEASE'
		queryDslVersion = '4.2.0' /* QueryDSL */
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
//        classpath("io.franzbecker:gradle-lombok:1.8")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
//apply plugin: 'io.franzbecker.gradle-lombok'

group = 'com.seran'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
    querydslapt
}

/* Test Configuration */
test {
	ignoreFailures = true
}

dependencies {
	
	compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    
    /* Security Tool */
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.security:spring-security-jwt:1.0.9.RELEASE')
    compile('com.auth0:java-jwt:3.3.0')
    
    /* Data-processing Tool */
    compile('com.fasterxml.jackson.core:jackson-databind')
	compile('org.assertj:assertj-core:3.10.0')
	compile('org.json:json:20180130')
	
	/* QueryDSL : unified queries tool */
	compile("com.querydsl:querydsl-core:$queryDslVersion")
    compile("com.querydsl:querydsl-jpa:$queryDslVersion")
    compile("com.querydsl:querydsl-apt:$queryDslVersion")
    
    /* Lombok */
    compileOnly('org.projectlombok:lombok')
    
    /* MySQL connection Tool */
    runtime('mysql:mysql-connector-java')
	runtime("com.h2database:h2")
	
    /* Test Tool */
    testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	
}

/* QueryDSL Configuration */
// The path of created QueryDSL file
sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/main/querydsl_generated'
        }
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build') {
    // The path of JPA entity file
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor,lombok.launch.AnnotationProcessorHider"
    ]
    destinationDir = file('./src/main/querydsl_generated')
}
compileJava.dependsOn(generateQueryDSL)

/* IDEA config */
idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
        sourceDirs += file('src/main/querydsl_generated')
    }
}
/* clean config */
clean {
    delete file('./src/main/querydsl_generated')
}